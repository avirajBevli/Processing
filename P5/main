// Rubiks Cube 3
// Daniel Shiffman
// https://thecodingtrain.com/CodingChallenges/142.3-rubiks-cube.html
// https://youtu.be/8U2gsbNe1Uo

import peasy.*;

PeasyCam cam;

float speed = 0.05;
int dim = 3;
Cubie[] cube = new Cubie[dim*dim*dim];

//Define 12 moves
Move[] allMoves = new Move[] {
  new Move(0, 1, 0, 1), 
  new Move(0, 1, 0, -1), 
  new Move(0, -1, 0, 1), 
  new Move(0, -1, 0, -1), 
  new Move(1, 0, 0, 1), 
  new Move(1, 0, 0, -1), 
  new Move(-1, 0, 0, 1), 
  new Move(-1, 0, 0, -1), 
  new Move(0, 0, 1, 1), 
  new Move(0, 0, 1, -1), 
  new Move(0, 0, -1, 1), 
  new Move(0, 0, -1, -1) 
};

ArrayList<Move> sequence = new ArrayList<Move>();
int counter = 0;

boolean started = false;

Move currentMove;

void setup() {
  size(600, 600, P3D);
  //fullScreen(P3D);
  cam = new PeasyCam(this, 400);
  int index = 0;
  for (int x = -1; x <= 1; x++) {
    for (int y = -1; y <= 1; y++) {
      for (int z = -1; z <= 1; z++) {
        PMatrix3D matrix = new PMatrix3D();
        matrix.translate(x, y, z);
        cube[index] = new Cubie(matrix, x, y, z);
        index++;
      }
    }
  }

  for (int i = 0; i < 25; i++) {
    int r = int(random(allMoves.length));
    Move m = allMoves[r];
    sequence.add(m);
  }

  currentMove = sequence.get(counter);

  for (int i = sequence.size()-1; i >= 0; i--) {
    Move nextMove = sequence.get(i).copy();
    nextMove.reverse();
    sequence.add(nextMove);
  }

  //Now that the list of moves, "sequence" is complete, we start implementing those moves
  //currentMove is the first move
  currentMove.start();
}

void draw() {
  background(51); //clear the stuff drawn in the previous iteration of draw loop

  //Permit the use of heads up display
  cam.beginHUD();
  fill(255);
  textSize(32);
  text(counter, 100, 100);
  cam.endHUD();

  //So that when starting we aren't looking at one face only
  rotateX(-0.5);
  rotateY(0.4);
  rotateZ(0.1);
  
  //Keep updating currentMove.angle until it becomes more than pi/2
  //After which we finish and give order to turnZ(z, dir)
  //turnZ will make sure all the correct cubies and their faces turn appropriately
  currentMove.update();
  
  //If the currentMove is finished, then start the next move but we have not updated
  //updated after starting it yet
  if (currentMove.finished()) {
    if (counter < sequence.size()-1) {
      counter++;
      currentMove = sequence.get(counter);
      currentMove.start();
    }
  }

  scale(50);
  //Update all the 27 cubies according to the current move
  for (int i = 0; i < cube.length; i++) {
    
    //So that once this move is done for a particular cubie, we come back to the original axis system
    push();
    
    //float theeta;
    //theeta = currentMove.angle;
    //If the cubie in question in question is aligned with the direction of movement scheduled in this move, move it
    if (abs(cube[i].z) > 0 && cube[i].z == currentMove.z) {
      rotateZ(currentMove.angle);
      //rotateZ(theeta);
    } else if (abs(cube[i].x) > 0 && cube[i].x == currentMove.x) {
      rotateX(currentMove.angle);
      //rotateX(theeta);
    } else if (abs(cube[i].y) > 0 && cube[i].y == currentMove.y) {
      rotateY(-currentMove.angle);
      //rotateY(-theeta);
    }   
    cube[i].show();//show the cubie after making the temporary transformation
    //that was done just to be able to show the intermediate states in animation
   
    pop();
    
  }
}
